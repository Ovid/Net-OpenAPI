#!/usr/bin/env perl

use Net::OpenAPI::Policy;
use Net::OpenAPI::Utils::Core qw(tidy_code);
use Data::Dumper;

my $base      = 'Net::OpenAPI::Exceptions::HTTP::';
my $base_path = 'Net/OpenAPI/Exceptions/HTTP/';

my %exception_for;
my %descriptions;
while ( my $status = <DATA> ) {
    chomp($status);
    my ( $code, $message ) = split ' ' => $status, 2;
    my $postfix = $message;
    $postfix =~ s/ //g;
    $postfix =~ s/\W//g;

    my $package      = "$base$postfix";
    my $test_package = "TestsFor::$base$postfix";
    my $path         = "lib/$base_path$postfix.pm";
    my $test_path    = "t/tests/TestsFor/$base_path$postfix.pm";
    $exception_for{$code} = $package;
    $descriptions{$code}  = [ $message, $package ];

    open my $fh, '>', $path;
    say {$fh} code( $package, $code, $message );
    open my $test_fh, '>', $test_path;
    say {$test_fh} test( $test_package, $code, $message );
}

my $exception_class = exception_class( $base, \%exception_for, \%descriptions );
open my $fh, '>', 'lib/Net/OpenAPI/App/Exceptions.pm';
print {$fh} tidy_code($exception_class);

sub exception_class {

    # yeah, this is getting very sloppy. Should fix it at some point
    my ( $base, $exception_for, $descriptions ) = @_;
    my $use_exceptions = join "\n" => map {"use $_;"} values %$exception_for;

    my $code_for = {};
    foreach my $exception ( values %$exception_for ) {
        my $short_name = $exception;
        $short_name =~ s/^$base//;
        $code_for->{$short_name} = $exception;
    }

    my $code = <<"END";
package Net::OpenAPI::App::Exceptions;

# ABSTRACT: Easily thrown HTTP exceptions

# warning: this code is auto-generated by $0
# manual changes here will be lost
use Net::OpenAPI::Policy;
use base 'Exporter';
our \@EXPORT_OK = qw(throw);

END
    local $Data::Dumper::Indent    = 1;
    local $Data::Dumper::Sortkeys  = 1;
    local $Data::Dumper::Quotekeys = 0;
    $exception_for = Data::Dumper->Dump( [$exception_for], ['*EXCEPTION_FOR_CODE'] );
    $code_for      = Data::Dumper->Dump( [$code_for],      ['*EXCEPTION_FOR_SHORT_NAME'] );

    $code .= "$use_exceptions\n\nmy $exception_for\nmy $code_for\n";

    $code .= <<'END';
sub throw {
    my ( $exception, $message ) = @_;
    $message //= 'No additional information';

    my $class;
    if ( $exception =~ /^\d\d\d$/a ) {
        $class = $EXCEPTION_FOR_CODE{$exception};
    }
    else {
        $class = $EXCEPTION_FOR_SHORT_NAME{$exception};
    }
    unless ($class) {
        croak("I don't know how to throw this exception: $exception ($message)");
    }
    $class->throw($message);
}

1;

__END__

=head1 SYNOPSIS

    use Net::OpenAPI::Exceptions qw(throw);

    throw( 500           => 'I messed up!' );
    throw( NotAcceptable => 'You messed up!' );
    throw(404);    # message is always optional

=head1 DESCRIPTION

This module provides one function, C<throw> to throw simple HTTP exceptions.

You pass C<throw> either a three-digit error code or a "short name" of the
exception.  An optional (but recommended) message may be passed as the second
argument.

Exceptions thrown are C<Net::OpenAPI::Exceptions::Base> exceptions.

=head1 SHORT NAMES

The following short names are provided.

END

    # $descriptions{$code} = [$message, $package];
    my $short_names = '';
    foreach my $code ( sort keys %$descriptions ) {
        my ( $message, $package ) = @{ $descriptions->{$code} };
        my $short = $package;
        $short =~ s/^$base//;
        $short_names .= <<"END";
=head2 C<$short>

Code:            $code
Description:     '$message'
Exception Class: L<$package>

END
    }
    $code .= $short_names;

    return $code;
}

sub test {
    my ( $package, $code, $message ) = @_;
    return <<"END";
package $package;

use Test::Class::Moose extends => 'Test::Net::OpenAPI';
use Moose;
with 'Test::Net::OpenAPI::Exceptions::Role::HTTP';

sub status_code {$code}
sub message     {'$message'}

__PACKAGE__->meta->make_immutable;
END
}

sub code {
    my ( $package, $code, $message ) = @_;
    return <<"END";
package $package;

# ABSTRACT: Exception class for the HTTP '$code $message' error

use Moo;
extends 'Net::OpenAPI::Exceptions::Base';
with 'Net::OpenAPI::Exceptions::Role::HTTP';
use overload '""' => 'to_string', fallback => 1;

=head1 IMPLEMENTS

This class implements the 'Net::OpenAPI::Exceptions::Role::HTTP' role.

=head1 METHODS

=head2 C<status_code>

    my \$code = \$self->status_code;

Returns $code.

=cut

sub status_code { $code }

=head2 C<message>

    my \$message = \$self->message;

Returns '$message'.

=cut

sub message { '$message' }

1;
END
}

__DATA__
300 Multiple Choices
301 Moved Permanently
302 Found
303 See Other
304 Not Modified
305 Use Proxy
307 Temporary Redirect
308 Permanent Redirect
400 Bad Request
401 Unauthorized
402 Payment Required
403 Forbidden
404 Not Found
405 Method Not Allowed
406 Not Acceptable
407 Proxy Authentication Required
408 Request Timeout
409 Conflict
410 Gone
411 Length Required
412 Precondition Failed
413 Payload Too Large
414 Request-URI Too Long
415 Unsupported Media Type
416 Requested Range Not Satisfiable
417 Expectation Failed
421 Misdirected Request
422 Unprocessable Entity
423 Locked
424 Failed Dependency
426 Upgrade Required
428 Precondition Required
429 Too Many Requests
431 Request Header Fields Too Large
444 Connection Closed Without Response
451 Unavailable For Legal Reasons
499 Client Closed Request
500 Internal Server Error
501 Not Implemented
502 Bad Gateway
503 Service Unavailable
504 Gateway Timeout
505 HTTP Version Not Supported
506 Variant Also Negotiates
507 Insufficient Storage
508 Loop Detected
510 Not Extended
511 Network Authentication Required
599 Network Connect Timeout Error
